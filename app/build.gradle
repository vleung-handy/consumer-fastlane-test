apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    signingConfigs {
        stage {
            keyAlias 'HandyStageKey'
            keyPassword 'S0H@ndy!'
            storeFile file('handy-stage-key.keystore')
            storePassword 'S0H@ndy!'
        }
        prod {
            keyAlias 'HandyProdKey'
            keyPassword 'S0H@ndy!'
            storeFile file('handy-prod-key.keystore')
            storePassword 'S0H@ndy!'
        }
        debug {
            keyAlias 'HandyProdKey'
            keyPassword 'S0H@ndy!'
            storeFile file('handy-prod-key.keystore')
            storePassword 'S0H@ndy!'
        }
    }
    compileSdkVersion 25
    buildToolsVersion '25.0.3'
    defaultConfig {
        applicationId 'com.handy.handy'
        minSdkVersion 16
        targetSdkVersion 25
        versionName '1.58.1'
        versionCode computeVersionCode(versionName)
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        //can't use CoreTestRunner for now because it prevents animations from being disabled on AWS
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
    }
    productFlavors {
        stage {
            applicationId 'com.handy.handy.stage'
            signingConfig signingConfigs.stage
            manifestPlaceholders = [PACKAGE_NAME: "com.handy.handy.stage"]
        }
        prod {
            applicationId 'com.handy.handy.prod'
            signingConfig signingConfigs.prod
            manifestPlaceholders = [PACKAGE_NAME: "com.handy.handy.prod"]
        }

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def buildInfo = "${flavorName}-${variant.buildType.name}"
                def versionInfo = "${variant.versionName}-${variant.versionCode}"
                output.outputFile = new File(output.outputFile.parentFile,
                        "handy-${buildInfo}-${versionInfo}.apk");
            }
        }
    }
    testOptions {
        unitTests.all {
            jvmArgs '-noverify'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    useLibrary 'org.apache.http.legacy'

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/library-res'
            ]
        }
    }
}

ext {
    supportLibraryVersion = '25.3.1'
    googlePlayServicesVersion = '11.0.1'
    firebaseVersion = '11.0.1'
}


repositories {
    maven { url "https://raw.githubusercontent.com/layerhq/releases-android/master/releases/" }
    maven { url "https://raw.githubusercontent.com/layerhq/Atlas-Android/master/releases/" }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/nineoldandroids-2.2.0.jar')
    compile files('libs/yozio-android-sdk.jar')

    //Mixpanel, remove once we figure out where are we losing events
    compile "com.mixpanel.android:mixpanel-android:4.9.2"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.3@aar') {
        transitive = true;
    }

    // Android Support Library
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:support-v13:$supportLibraryVersion"
    compile "com.android.support:support-annotations:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:cardview-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:animated-vector-drawable:$supportLibraryVersion"
    compile "com.android.support:customtabs:$supportLibraryVersion"
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Google
    compile "com.google.android.gms:play-services-gcm:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-basement:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-base:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-identity:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-wallet:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-location:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-analytics:$googlePlayServicesVersion"
    compile "com.google.android.gms:play-services-ads:$googlePlayServicesVersion"
    compile 'com.google.code.findbugs:jsr305:3.0.1'
    compile "com.google.firebase:firebase-core:$firebaseVersion"
    compile "com.google.firebase:firebase-crash:$firebaseVersion"
    compile "com.google.firebase:firebase-messaging:$firebaseVersion"
    compile 'com.google.guava:guava:20.0'

    // Square
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.7.5'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'

    // Jake Wharton
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'
    compile 'com.github.JakeWharton:ViewPagerIndicator:2.4.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    compile 'com.wdullaer:materialdatetimepicker:3.1.3'
    compile 'io.card:android-sdk:5.3.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.stripe:stripe-android:1.0.4'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.scottyab:secure-preferences-lib:0.1.1'
    compile 'com.facebook.android:facebook-android-sdk:4.16.1'
    compile 'com.airbnb:deeplinkdispatch:1.5.0'
    annotationProcessor 'com.airbnb:deeplinkdispatch-processor:1.5.0'
    compile 'com.usebutton:android-sdk:5.0.1'
    compile 'org.apmem.tools:layouts:1.10@aar'

    // Confetti
    compile 'com.plattysoft.leonids:LeonidsLib:1.3.2'

    //user-tracking sdk that allows us to record the screen
    compile 'com.appsee:appsee-android:2.3'

    compile 'com.urbanairship.android:urbanairship-sdk:6.4.1'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.1.6'

    //layer chat
    compile 'com.layer.atlas:layer-atlas:0.3.5'
    compile('com.layer.sdk:layer-sdk:0.23.4') {
        exclude group: 'com.google.firebase', module: 'firebase-messaging'
        exclude group: 'com.google.firebase', module: 'firebase-core'
    }
    compile(name: 'handy-shared', ext: 'aar')

    //leak canary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'


    //Testing
    testCompile 'org.robolectric:robolectric:3.3.2'
    testCompile 'org.robolectric:shadows-support-v4:3.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'org.robolectric:shadows-multidex:3.0'

    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        // Necessary to avoid version conflicts
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }

}

dexcount {
    format = "list"
    includeClasses = false
    includeFieldCount = false
    includeTotalMethodCount = false
    orderByMethodCount = false
    verbose = false
    maxTreeDepth = Integer.MAX_VALUE
    teamCityIntegration = false
    enableForInstantRun = false
    teamCitySlug = null
}

def computeVersionCode(versionName) {
    def versionNameMatcher = (versionName =~ /^([1-9]\d*)\.(\d{1,2})\.(\d{1,2})$/)
    if (versionNameMatcher.matches()) {
        def identifiers = (versionNameMatcher[0] - [versionName])
        def (major, minor, patch) = identifiers.collect { it.toInteger() }
        def versionCode = (major * 1000000) + (minor * 10000) + (patch * 100)

        def out = services.create("debug")
        out.println("\nBuilding version ${versionName} (${versionCode})\n")
        return versionCode
    } else {
        throw new InvalidUserDataException("Illegal version name ${versionName}")
    }
}

apply plugin: 'com.google.gms.google-services'
